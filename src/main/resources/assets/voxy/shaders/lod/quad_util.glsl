//Common utility functions for decoding and operating on quads

vec3 swizzelDataAxis(uint axis, vec3 data) {
    //Metal fix: bool-select mix() overload miscompiles via SPIRV-Cross MSL (see frustum.glsl)
    return axis==1 ? data : (axis==0 ? data.xzy : data.zxy);
}

uint extractDetail(uvec2 encPos) {
    return encPos.x>>28;
}

ivec3 extractLoDPosition(uvec2 encPos) {
    //Metal fix: (v<<L)>>R sign-extension shifts miscompile via SPIR-V->MSL (see screenspace.glsl); bitfieldExtract is well-defined
    int y = bitfieldExtract(int(encPos.x), 20, 8);
    int x = bitfieldExtract(int(encPos.y), 4, 24);
    int z = bitfieldExtract(int(((encPos.x&((1u<<20)-1))<<4)|(encPos.y>>28)), 0, 24);
    return ivec3(x,y,z);
}

vec4 getFaceSize(uint faceData) {
    float EPSILON = 0.00005f;

    vec4 faceOffsetsSizes = extractFaceSizes(faceData);

    //Expand the quads by a very small amount (because of the subtraction after this also becomes an implicit add)
    faceOffsetsSizes.xz -= vec2(EPSILON);

    //Make the end relative to the start
    faceOffsetsSizes.yw -= faceOffsetsSizes.xz;

    return faceOffsetsSizes;
}


vec2 taaOffset = vec2(0);//TODO: compute this

struct QuadData {
    uvec4 attributeData;

    float lodScale;
    uint axis;
    //Used for computing the 4 corners of the quad
    vec3 basePoint;
    vec2 quadSizeAddin;
    vec2 uvCorner;
};

uint makeQuadFlags(uint faceData, uint modelId, ivec2 quadSize, const in BlockModel model, uint face) {
    //bit: 0-use cuttout, 1-dont use mipmaps, 2|3-tint state, 4|6-face, 8|11-width, 12|15-height, 16|31-model id
    uint flags = 0;

    flags |= modelId<<16;//Model id
    flags |= (uint(quadSize.x-1)<<8)|(uint(quadSize.y-1)<<12);//quad size

    {//Cuttout
        flags |= faceHasAlphaCuttout(faceData);
        flags |= uint(any(greaterThan(quadSize, ivec2(1)))) & faceHasAlphaCuttoutOverride(faceData);
    }

    //TODO: remove, there is no non mip code path anymore
    //flags |= uint(!modelHasMipmaps(model))<<1;//Not mipmaps

    flags |= faceTintState(faceData)<<2;
    flags |= face<<4;//Face

    return flags;
}

uint packVec4(vec4 vec) {
    uvec4 vec_=uvec4(vec*255)<<uvec4(24,16,8,0);
    return vec_.x|vec_.y|vec_.z|vec_.w;
}


#ifndef PATCHED_SHADER
float computeDirectionalFaceTint(bool isShaded, uint face);
#endif

uvec3 makeRemainingAttributes(const in BlockModel model, const in Quad quad, uint lodLevel, uint face) {
    uvec3 attributes = uvec3(0);

    uint lighting = extractLightId(quad);

    //Apply model colour tinting
    uint tintColour = model.colourTint;

    if (modelHasBiomeLUT(model)) {
        tintColour = colourData[tintColour + extractBiomeId(quad)];
    }

    #ifdef PATCHED_SHADER
    attributes.x = lighting;
    attributes.y = tintColour;
    #else
    bool isTranslucent = modelIsTranslucent(model);

    //afak, these are the same variable in vanilla, (i.e. shaded == ao)
    bool isShaded = modelIsShaded(model);
    bool hasAO = isShaded;

    // VOXY_LOD_NO_VERTEX_LIGHT=1 -- a COST probe, not a feature. Replaces the vertex stage's lightmap
    // sample with a constant, so every other line of the vertex shader (quad setup, the position fetch,
    // the face tint, the packing) still runs and only the dependent texture fetch disappears.
    //
    // Why it matters: the LOD pass costs ~16 ms of GPU time, and two separate measurements have now
    // removed the other candidates -- a zero scissor (no fragments at all) leaves it unchanged, and an
    // ICB (no per-draw setVertexBytes) leaves it unchanged. What survives is vertex work, and this is
    // the largest single term in it. The rendered image is wrong (uniform lighting) by design.
    #ifdef VOXY_LOD_NO_VERTEX_LIGHT
    vec4 tinting = vec4(1.0);
    #else
    vec4 tinting = getLighting(lighting);
    #endif

    uint conditionalTinting = 0;
    if (tintColour != uint(-1)) {
        conditionalTinting = tintColour;
    }

    uint addin = 0;
    if (!isTranslucent) {
        tinting.w = 0.0;
        //Encode the face, the lod level and
        uint encodedData = 0;
        encodedData |= face;
        encodedData |= (lodLevel<<3);
        encodedData |= uint(hasAO)<<6;
        addin = encodedData;
    }

    tinting.rgb *= computeDirectionalFaceTint(isShaded, face);

    attributes.x = packVec4(tinting);
    attributes.y = conditionalTinting;
    attributes.z = addin|(face<<8);
    #endif

    #ifdef VOXY_LOD_SHOW_LIGHT
    // Diagnostic (VOXY_LOD_SHOW_LIGHT=1): `lighting` is the raw byte
    // VoxelIngestService.getLightingSupplier wrote into the voxel -- sky in the low nibble,
    // block in the high. Both the lightmap sample above and every downstream colour term are
    // lossy, so carry the byte itself to the fragment stage in bits 24-31 of attributes.z,
    // which no reader touches (addin owns 0-6, face 8-10, dist-mip 16-18).
    attributes.z |= (lighting & 0xFFu) << 24;
    #endif

    #ifdef VOXY_LOD_DIST_MIP
    // Distance-mip (Metal): the fragment stage needs the quad's LOD scale to
    // turn view distance into an atlas mip level. `addin` only carries the
    // lod level for OPAQUE quads (translucent leaves it 0, and quads.frag
    // adds interData.w&0xFF to alpha, so the low byte is off-limits). Pack it
    // in bits 16-18, which no reader touches on any backend. Applies to BOTH
    // the base and PATCHED (vx material) attribute layouts — PATCHED leaves
    // attributes.z at 0, and without these bits its dist-mip would compute
    // texel size with lodScale=1 and over-blur every far quad.
    attributes.z |= (lodLevel&7u)<<16;
    #endif

    return attributes;
}

void setupQuad(out QuadData quad, const in Quad rawQuad, uvec2 sPos, bool generateAttributes) {
    uint lodLevel = extractDetail(sPos);
    float lodScale = 1<<lodLevel;
    //Metal fix: '<<' on negative position ints is UB (see screenspace.glsl); use multiplication
    ivec3 baseSection = (extractLoDPosition(sPos)*(1<<lodLevel)) - baseSectionPos;

    uint face = extractFace(rawQuad);
    uint modelId = extractStateId(rawQuad);
    // The quad's own faceModelData, baked by the mesher into the element's third int, with bit 31 as
    // "this value is real". When the flag is clear the model was not baked yet when this quad was
    // meshed, so fall back to loading it -- which is exactly what this shader did before the bake
    // existed, and therefore renders the same as it always did.
    //
    // The flag is load-bearing, not belt-and-braces. The shader's old read of modelData[modelId]
    // happened at RENDER time, when the model necessarily existed; the mesher's read happens at MESH
    // time, which for a section meshed during world load can precede the bake. Latching a 0 there
    // produced a degenerate uvCorner and black terrain -- caught by a bake-vs-pre-bake screenshot diff,
    // where the pre-bake control was clean and the bake arm was not.
    //
    // faceModelData == 0 is a sound "not baked" test: bits 0..15 are faceSize[0..3] scaled to 0..15 and
    // a real face has faceSize[2] and faceSize[3] at least 1, so a genuine face is never 0. An empty
    // face is -1, which has bit 31 set and matches the -1 the model would have given.
    uint rawQuadFaceData = uint(rawQuad.z);
    uint faceData = (rawQuadFaceData & 0x80000000u) != 0u
            ? rawQuadFaceData
            : modelData[modelId].faceData[face];
    ivec2 quadSize = extractSize(rawQuad);

    if (generateAttributes) {
        BlockModel model = modelData[modelId];
        quad.attributeData.x = makeQuadFlags(faceData, modelId, quadSize, model, face);
        quad.attributeData.yzw = makeRemainingAttributes(model, rawQuad, lodLevel, face);
    }

    vec4 faceSize = getFaceSize(faceData);
    #ifdef USE_SINGLE_TRI
    faceSize *= 2;
    #endif
    vec3 quadStart = extractPos(rawQuad);
    float depthOffset = extractFaceIndentation(faceData);
    #ifdef VOXY_LOD_ABS_INDENT
    // Metal fix (2026-07-03): the model-space indent gets multiplied by
    // lodScale below (basePoint = quadStart*lodScale), so an indented face —
    // the water surface at 1-0.1094 being the critical one — sat 0.109*2^L
    // blocks below its cell top at level L: parent water planes floated up
    // to ~0.9 blocks ABOVE child planes. Consequences: stacked translucent
    // planes double-blending into pale section-aligned veils, an exposed
    // see-through gap band at every LOD ring transition, and mid/far water
    // at the wrong world height. Divide by lodScale so the indent stays
    // ABSOLUTE (in blocks) at every level. Metal-only define
    // (VOXY_LOD_ABS_INDENT, kill switch =0); GL keeps upstream scaling.
    depthOffset /= lodScale;
    #endif
    quadStart += swizzelDataAxis(face>>1, vec3(faceSize.xz, mix(depthOffset, 1-depthOffset, float(face&1u))));

    quad.lodScale = lodScale;
    quad.axis = face>>1;
    quad.basePoint = (quadStart*lodScale)+vec3(baseSection*32);//Metal fix: baseSection can be negative, see screenspace.glsl
    #ifdef USE_SINGLE_TRI
    quad.quadSizeAddin = (faceSize.yw + (quadSize - 1)*2);
    #else
    quad.quadSizeAddin = faceSize.yw + quadSize - 1;
    #endif
    quad.uvCorner = faceSize.xz;
}

vec4 getQuadCornerPos(in QuadData quad, uint cornerId) {
    vec2 cornerMask = vec2((cornerId>>1)&1u, cornerId&1u)*quad.lodScale;
    vec3 point = quad.basePoint + swizzelDataAxis(quad.axis,vec3(quad.quadSizeAddin*cornerMask,0));
    vec4 pos = MVP * vec4(point, 1.0f);
    pos.xy += taaOffset*pos.w;
    return pos;
}

#ifndef USE_NV_BARRY
vec2 getCornerUV(const in QuadData quad, uint cornerId) {
    return quad.uvCorner + quad.quadSizeAddin*vec2((cornerId>>1)&1u, cornerId&1u);
}
#endif

#ifndef PATCHED_SHADER
float computeDirectionalFaceTint(bool isShaded, uint face) {
    //Apply face tint
    if (isShaded) {
        //just index on a const array with the face as an index, will be much faster
        // or use a vector and select/sum
        // but per face might be easier?


        if ((face>>1) == 1) {//NORTH, SOUTH
            return Z_AXIS_FACE_TINT;
        } else if ((face>>1) == 2) {//EAST, WEST
            return X_AXIS_FACE_TINT;
        } else if (face == 1) {//UP
            return UP_FACE_TINT;
        }
        //DOWN
        return DOWN_FACE_TINT;
    } else {
        return NO_SHADE_FACE_TINT;
    }
}
#endif